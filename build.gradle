buildscript {
    repositories {
        mavenCentral()
	mavenLocal()
        mavenRepo name: 'clojars', url: 'http://clojars.org/repo'    
    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-android-plugin:1.1.1-SNAPSHOT'
        classpath 'clojuresque:clojuresque:1.4.4'       
    }
}

apply plugin: 'android'
apply plugin: 'clojure'
repositories {
    mavenCentral()
    mavenLocal()
    mavenRepo name: 'clojars', url: 'http://clojars.org/repo'    
}

// Sets the package group & version
group = 'lu.kreios'
version = "1.0.0"

// Some Clojure-specific configuration
compileClojure.dependsOn compileJava
warnOnReflection = true
aotCompile = true

dependencies {
    compile 'org.clojure:clojure:1.3.0'
    development 'swank-clojure:swank-clojure:1.3.4'
    development 'clj-stacktrace:clj-stacktrace:0.2.4'
}

// Configure the filtering of resources with properties from the Gradle's project scope
processResources {
    expand (project.properties)
}

// Configure a dedicated debug build
task configureDebug << {
    jar.classifier = "debug"
}
// Configure a dedicated release build
task configureRelease << {
    proguard.enabled = true
}

// Swank integration
task runSwank(type: clojuresque.ClojureExec, dependsOn: compileJava) {
    classpath = project.files(
        project.sourceSets.main.clojure.srcDirs,
        project.sourceSets.main.classesDir,
        project.sourceSets.test.clojure.srcDirs,
        project.configurations.testRuntime,
        project.configurations.development,
        project.ant.references['android.target.classpath'].list()
    )
    main = "swank.swank/start-repl"
    args = []
}

// The name of the emulated Android device to use
androidEmulatorStart {
    avdName = 'Android_2.3.3'
}
